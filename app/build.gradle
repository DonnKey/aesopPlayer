buildscript {
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

repositories {
    maven { url 'https://guardian.github.io/maven/repo-releases' } // for 'option'
    maven { url "https://jitpack.io" } // For CircularReveal
    google()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "github.io.donnKey.aesopPlayer"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 1
        versionName getVersionName()
    }
    buildTypes {
        debug {
            manifestPlaceholders = [doCrashReports:"true"]
            //TODO: the calls to Crashlytics.log (at least) will need to be conditional to allow this to
            //      be false.  Need to figure how to determine this setting from within app.
            //      See also use of this in AndroidManifest.
        }
        release {
            manifestPlaceholders = [doCrashReports:"true"]
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.multidex:multidex:2.0.1'

    // To successfully use Firebase you'll need to download google-services.json from
    // your Firebase account (it's custom for each Firebase project/app). It should not
    // be checked in publicly.
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation 'de.greenrobot:eventbus:2.4.1'

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'

    // implementation 'com.flurry.android:analytics:6.9.2' Disabled: see analytics/StatsLogger.java
    implementation 'com.google.guava:guava:24.0-android'
    implementation 'com.google.code.findbugs:jsr305:3.0.1'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.7.3'
    implementation 'com.google.dagger:dagger:2.15'

    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0-beta01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0-beta01'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    implementation 'com.github.ozodrukh:CircularReveal:2.0.1@aar'

    implementation 'com.mpatric:mp3agic:0.9.0'
    // 0.9.1 doesn't currently (3/19) run down-level due to Java lib usage

    implementation 'org.apache.commons:commons-text:1.6'
    implementation 'org.apache.commons:commons-compress:1.12'

    // https://github.com/guardian/Option - needed by dirchooser
    // dirchooser only needed 4.4 and below
    implementation 'com.gu:option:1.3'
    implementation 'net.rdrei.android.dirchooser:library:3.2@aar'
}
