buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" } // For CircularReveal
    google()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 27
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.studio4plus.homerplayer"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 41
        versionName getVersionName()
    }
    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'de.greenrobot:eventbus:2.4.1'

    compile 'com.android.support:appcompat-v7:27.1.1'
    compile 'com.android.support.constraint:constraint-layout:1.1.0'
    compile 'com.android.support:support-v13:27.1.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') { transitive = true }
    compile 'com.flurry.android:analytics:6.9.2'
    compile 'com.google.guava:guava:24.0-android'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.android.exoplayer:exoplayer-core:2.7.0'

    compile 'com.google.dagger:dagger:2.15'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'com.github.ozodrukh:CircularReveal:2.0.1@aar'

}